# procpkg.ndf

# created by Mike Jenkins
  September 15, 1995
  Last modified by Mike March 2017.

# This file processes the global initalization files to produce
     basics.c
     basics.h
     defstbl.h
     pkgswchs.h
appropriate to build a full nial executable from the core version.
It creates these in the ../src directory.

settrigger o;  % avoid faults for directory creation failures;

geninitcall IS OP Primname Prop {
   link 'init_primname("' Primname '",''' Prop ''');' }

insertslash is OP Str {
    EscapeChars := '"\';
    NewStr := Null;
    FOR c WITH Str DO
      IF c in EscapeChars THEN
        NewStr := NewStr append `\;
      ENDIF;
      NewStr := NewStr append c;
    ENDFOR;
    NewStr
}

# Definitions from defs.ndf needed to build from nialcore

OS_Path_Separator := os_get_parameter 1;

FILTER IS TRANSFORMER f OPERATION A { EACH f A sublist A }

flip is pack

last IS OPERATION A {tally A - 1 pick list A}


# the following routines are copied from niallib.

newhost IS OPERATION Comm {  
       Svt := settrigger o ; 
       Fh := open Comm 'pr' ; 
       IF not isfault Fh THEN 
          Res := Null ; 
          WHILE not isfault ( Line := readfile Fh ) DO 
             Res := Res append Line ; 
          ENDWHILE ; 
          close Fh ; 
          settrigger Svt ; 
          Res
       ELSE 
          settrigger Svt ; 
          fault ( link '?Failed to open pipe to: ' Comm ) 
       ENDIF 
     } 

filelist IS OPERATION Path Pattern { 
   Cmd := link '\ls -da ' Path OS_Path_Separator ( string Pattern );
   Nms := newhost Cmd;
   IF not empty Nms THEN
     last (first OS_Path_Separator findall (first Nms)+1) EACHRIGHT drop Nms
   ELSE
     Nms
   ENDIF
} 

filelist1 IS OPERATION Pattern {  
      newhost ( link '\ls -da ' ( string Pattern ) ) 
} 


# routine to build the table entries

mkstrtbl IS OP Tblnm Defs Isstatic {
  Lines := EACH insertslash Defs;
  Lines := '"' EACHRIGHT link Lines EACHLEFT link '",';
  Tbl := link 
           [link '#define NOLINES ' (string tally Lines),'']
           [ link (IF Isstatic THEN 'static ' ELSE '' ENDIF)
                'char *' (string Tblnm) '[' (string tally Lines) '] = {' ] 
           Lines 
           ['};'];
  Tbl };


  unpad IS OP st { parts :=  (match st ` ) cut st;
                   link ((first parts) (eachleft link (rest parts) ' '))
                   }; 


spliton IS OP splitter str {
  (match splitter@0 str) cut str 
}



Buildcore IS {
% This section processes the names of Q'Nial primitives in the core;
% These are gathered from coreprims.nh ;
  Entries := getfile "coreprims.nh;
% Each primitive is included by placing the primitive name(s) in the;
% apply table(s) and by generating a call on init_primname for;
% each to be included in the initialization code;
% and to initialize dispatch tables and lists ;
  Unarydisp := [ 'void (*applytab[])() = {' ];
  Binarydisp := [ 'void (*binapplytab[])() = {' ];
  Primlist := Null;
  Nmlist := Null;
  Initlist := Null; 
  Entries := FILTER ([]~=) Entries;
  Entries := FILTER (`#~=first) Entries;
  Primlistvalid := true;
  FOR I WITH tell tally Entries DO
    Pieces := spliton ' ' Entries@I;
    Feature Prop Primname Ufname := 4 take Pieces;
    IF Primname in Primlist THEN
      write 'ERROR: duplicate primitive name' Primname;
      Primlistvalid := false;
      EXIT fault('?duplicate primitive name: ' link Primname);
    ENDIF;
    Primlist := Primlist append Primname;
    Nmlist := Nmlist append Ufname;
    Unarydisp := Unarydisp append (Ufname link ',');
    Initlist := Initlist append geninitcall (toupper Primname) (first Prop);
    IF tally Pieces > 4 THEN
        Bfname :=  last Pieces; 
        Binarydisp := Binarydisp append (Bfname link ',');
        Nmlist := Nmlist append Bfname;
    ENDIF;
  ENDFOR;
 IF Primlistvalid THEN
%  Complete the dispatchers and generate the initprims routine:;
  Unarydisp := Unarydisp link ['};'];
  Binarydisp := Binarydisp link ['};'];
  Initprims := link [ 'void initprims()', '{' ] Initlist ['}'];
%  Now build the lines for basics.c;
  Basicsc := link [
    '/*================================================================',
    '',
    '                MODULE BASICS.C     ',
    '',
    '      This module contains the unary and binary dispatch tables used to',
    '      access all the Nial primitives that are written in C.',
    '      It also contains the routine that dynamically links the Nial',
    '      name for the primitive to its slot in the dispatch table.',
    '',
    '================================================================',
    '',
    'GENERATED by buildfromcore.ndf',
    '',
    '================================================================*/',
    '',
   '#include "basics.h"',
   ''] [''] Unarydisp [''] Binarydisp [''] Initprims ;
%  output basics.c ;
  putfile '../src/basics.c' Basicsc;
% process the name list to create basics.h info;
  Basicsh := [
    '/* BASICS.H */',
    '',
    'extern void init_primname(char * primname,char prop);',
    'extern void initprims(void);',
    'extern void (*applytab[]) (void);',
    'extern void (*binapplytab[]) (void);',
    ''];
  FOR Nm WITH Nmlist DO
    Basicsh := Basicsh link [link 'extern void ' Nm '(void);'];
  ENDFOR;
  putfile '../src/basics.h' Basicsh;
  Defs := getfile 'nialcoredefs.ndf';
  putfile '../src/defs.ndf' Defs;
%  work to build the defstbl.h file used by nial.c in initialization;
  Defstbl := link [
    '/*==================================================================',
    '',
    '            defstbl.h',
    '',
    '   GENERATED by buildfromcore.ndf',
    '',
    '==================================================================*/',
    '' ] 
   (mkstrtbl 'defsndf' Defs True);
  putfile '../src/defstbl.h' Defstbl;
%  work to build the package switches file;
%  it has no switches in the basic version;
  Pkgswchs := [
    '/*==================================================================',
    '',
    '            pkgswchs.h',
    '',
    '   GENERATED by buildfromcore',
    '',
    '==================================================================*/',
    '' ];
  putfile '../src/pkgswchs.h' Pkgswchs;
% copy core source files to src dir ;
  newhost 'cp src_nialcore/* ../src';
%  work to build the CMakeLists.txt file;
  Cmaketext := getfile 'Cmakeheader';
  Cmaketext := Cmaketext link getfile 'Cmakemiddle';
  Cmaketext := Cmaketext link getfile 'Cmaketrailer';
  putfile '../src/CMakeLists.txt' Cmaketext;
  write link 'Completed work to generate source code for basic nial: ';
  ENDIF
} 


Buildcore 

Bye




